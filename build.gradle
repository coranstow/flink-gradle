plugins {
    id 'java'
    id 'application'
    // shadow plugin to produce fat JARs
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "com.github.davidmc24.gradle.plugin.avro" version "1.7.1"
    id "idea"
}

// artifact properties
group = 'org.example'
version = '0.1-SNAPSHOT'
mainClassName = 'org.example.PizzaOrdersAggregator'
description = """Flink Quickstart Job"""
ext {
    javaVersion = '1.8'
    flinkVersion = '1.17.1'
    jacksonVersion = '2.13.4'
    scalaBinaryVersion = '_2.12'
    slf4jVersion = '1.7.36'
    log4jVersion = '2.17.1'
    avroVersion = '1.11.1'
}
sourceCompatibility = javaVersion
targetCompatibility = javaVersion
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
applicationDefaultJvmArgs = ["-Dlog4j.configurationFile=log4j2.properties"]

// declare where to find the dependencies of your project
repositories {
    mavenCentral()
//    maven {
//        url "https://repository.apache.org/content/repositories/snapshots"
//        mavenContent {
//            snapshotsOnly()
//        }
//    }
    maven {
        url "https://packages.confluent.io/maven"
    }
}

// NOTE: We cannot use "compileOnly" or "shadow" configurations since then we could not run code
// in the IDE or with "gradle run". We also cannot exclude transitive dependencies from the
// shadowJar yet (see https://github.com/johnrengelman/shadow/issues/159).
// -> Explicitly define the // libraries we want to be included in the "flinkShadowJar" configuration!
configurations {
    flinkShadowJar // dependencies which go into the shadowJar
    // always exclude these (also from transitive dependencies) since they are provided by Flink
    flinkShadowJar.exclude group: 'org.apache.flink', module: 'force-shading'
    flinkShadowJar.exclude group: 'com.google.code.findbugs', module: 'jsr305'
    flinkShadowJar.exclude group: 'org.slf4j'
    flinkShadowJar.exclude group: 'org.apache.logging.log4j'

}


// declare the dependencies for your production and test code
dependencies {
    // --------------------------------------------------------------
    // Compile-time dependencies that should NOT be part of the
    // shadow (uber) jar and are provided in the lib folder of Flink
    // --------------------------------------------------------------
    implementation "org.apache.flink:flink-streaming-java:${flinkVersion}"
    implementation "org.apache.flink:flink-clients:${flinkVersion}"
//    implementation "org.apache.flink:flink-connector-kafka:${flinkVersion}"

//    implementation "org.apache.flink:flink-avro-confluent-registry:${flinkVersion}"



    // --------------------------------------------------------------
    // Dependencies that should be part of the shadow jar, e.g.
    // connectors. These must be in the flinkShadowJar configuration!
    // --------------------------------------------------------------

    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    implementation 'org.jetbrains:annotations:24.0.0'
    flinkShadowJar "org.apache.flink:flink-avro:${flinkVersion}"
    flinkShadowJar "org.apache.flink:flink-formats:${flinkVersion}"
    flinkShadowJar "org.apache.flink:flink-connector-kafka:${flinkVersion}"
    flinkShadowJar "org.apache.flink:flink-avro-confluent-registry:${flinkVersion}"
    flinkShadowJar "org.apache.avro:avro:${avroVersion}"
//    flinkShadowJar "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
//    flinkShadowJar "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
//    flinkShadowJar "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    flinkShadowJar "io.confluent:kafka-schema-registry-client:7.1.3"
    flinkShadowJar "org.json:json:20230618"


    // Add test dependencies here.
    testImplementation(platform("org.junit:junit-bom:5.9.1"))
    testImplementation("org.junit.jupiter:junit-jupiter")
}

// make compileOnly dependencies available for tests:
sourceSets {
    main.compileClasspath += configurations.flinkShadowJar
    main.runtimeClasspath += configurations.flinkShadowJar
    test.compileClasspath += configurations.flinkShadowJar
    test.runtimeClasspath += configurations.flinkShadowJar
    javadoc.classpath += configurations.flinkShadowJar

}
run.classpath = sourceSets.main.runtimeClasspath

jar {
    manifest {
        attributes 'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version')
    }
}

shadowJar {
    configurations = [project.configurations.flinkShadowJar]
}

tasks.test {
    useJUnitPlatform()
}